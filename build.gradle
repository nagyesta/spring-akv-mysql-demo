plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'com.avast.gradle.docker-compose' version '0.17.10'
}

group = 'com.epam.demo'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudAzureVersion', "5.18.0")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'com.azure.spring:spring-cloud-azure-starter-keyvault'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "com.azure.spring:spring-cloud-azure-dependencies:${springCloudAzureVersion}"
    }
}

test {
    useJUnitPlatform()
    systemProperty("spring.profiles.active", "dev")
    systemProperty("javax.net.ssl.trustStore",
            file("$projectDir/local/lowkey-vault/lowkey-vault-keystore.p12"))
    systemProperty("javax.net.ssl.trustStorePassword", "changeit")
    systemProperty("javax.net.ssl.trustStoreType", "PKCS12")
    // Only needed if Assumed Identity and DefaultAzureCredential is used to
    // simulate IMDS managed identity
    environment "IDENTITY_ENDPOINT",
            "http://localhost:10544/metadata/identity/oauth2/token"
    environment "IDENTITY_HEADER", "header"
    testLogging.showStandardStreams = true
}

bootRun {
    systemProperty("spring.profiles.active", "dev")
    systemProperty("javax.net.ssl.trustStore",
            file("$projectDir/local/lowkey-vault/lowkey-vault-keystore.p12"))
    systemProperty("javax.net.ssl.trustStorePassword", "changeit")
    systemProperty("javax.net.ssl.trustStoreType", "PKCS12")
    // Only needed if Assumed Identity and DefaultAzureCredential is used to
    // simulate IMDS managed identity
    environment "IDENTITY_ENDPOINT",
            "http://localhost:10544/metadata/identity/oauth2/token"
    environment "IDENTITY_HEADER", "header"
}
